apply plugin: 'com.android.application'

def APP_NAME = "Cases"

android {
    // 指定 properties 文件解析，如 local.properties
    Properties properties = new Properties()
    InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
    properties.load(inputStream)
    // properties.getProperty("KEY_APP_STOREFILE")

    compileSdkVersion 28
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.csp.cases"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"

        // 多渠道用
        flavorDimensions "applicationId"

        // 自定义 BuildConfig 常量
        buildConfigField "String", "DEV_USER", DEV_USER
    }

    // java 1.8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 自动签名
    signingConfigs {
        debug {
            storeFile file(KEY_APP_STOREFILE)
            storePassword KEY_STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }

        release {
            storeFile file(KEY_APP_STOREFILE)
            storePassword KEY_STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug // 自动签名
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // 多渠道
    productFlavors {
        // 正式版本
        publish {
        }

        // 开发/测试渠道：环境未知
        dev {
            // applicationIdSuffix ".dev" // 修改包名
        }
        dtest {
        }
    }

    // 多渠道配置
    applicationVariants.all { variant ->
        // 输出 apk 文件重命名
        variant.outputs.all {
            def now = new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC+8"))
            outputFileName = "app-${variant.buildType.name}-v${versionName}-${variant.flavorName}-${now}.apk"
        }

        buildConfigField "boolean", "IS_DEV", "false"
        buildConfigField "boolean", "IS_TEST", "false"
        buildConfigField "boolean", "IS_RELEASE", "false"

        switch (variant.flavorName) {
            case "dev":
            case "dtest":
                resValue "string", "app_name", "${APP_NAME}(${variant.flavorName})"
                buildConfigField "boolean", "IS_DEBUGGABLE", "true" // 请不要把 IS_DEBUGGABLE 改成 DEBUGGABLE，避免混淆

                def isTest = variant.flavorName.startsWith("dtest")
                buildConfigField "boolean", "IS_DEV", "${(!isTest)}"
                buildConfigField "boolean", "IS_TEST", "${isTest}"
                break
            default:
                buildConfigField "boolean", "IS_DEBUGGABLE", "${(variant.buildType.name == "debug")}"
                buildConfigField "boolean", "IS_RELEASE", "true"
                if (variant.buildType.name == "debug") {
                    resValue "string", "app_name", "${APP_NAME}(${variant.flavorName})"
                } else {
                    resValue "string", "app_name", "${APP_NAME}"
                }
                break
        }

        // 本项目不修改 app 名称
        resValue "string", "app_name", "${APP_NAME}"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:percent:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:multidex:1.0.3'

    implementation project(':customview')
    implementation project(':libraryforandroid')
    implementation project(':libraryforjava')
    implementation project(':utilsforandroid')

    compile files('libs/network/gson-2.8.0.jar')
    compile files('libs/network/volley.jar')

    // implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    // implementation 'com.squareup.okio:okio:1.14.1'

    compile files('libs/network/okhttp/okhttp-3.9.0.jar')
    compile files('libs/network/okhttp/okio-1.13.0.jar')

    compile files('libs/network/retrofit/retrofit-2.3.0.jar')
    compile files('libs/network/retrofit/converter-gson-2.3.0.jar')
    compile files('libs/network/retrofit/retrofit2-rxjava2-adapter-1.0.0.jar')

    compile files('libs/rxjava/rxjava-2.1.7.jar')
    compile files('libs/rxjava/rxandroid-2.0.1.jar')

    compile files('libs/reactive-streams-1.0.1.jar')
}
